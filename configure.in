
AC_PREREQ(2.57)
AC_INIT(main.c)
AC_CONFIG_HEADER(config.h)

# Checks for programs.
AC_PROG_CC
if test -n "$GCC"; then
   CFLAGS="$CFLAGS -Wall"
fi

AC_MSG_CHECKING(pctl type)
if test -r /dev/bpf0 ; then
        V_PCTL=bpf
elif test -r /usr/include/net/pfilt.h ; then
        V_PCTL=pf
elif test -r /dev/enet ; then
        V_PCTL=enet
elif test -r /dev/nit ; then
        V_PCTL=snit
elif test -r /usr/include/sys/net/nit.h ; then
        V_PCTL=nit
elif test -r /usr/include/net/raw.h ; then
        V_PCTL=snoop
elif test -r /usr/include/sys/dlpi.h ; then
        V_PCTL=dlpi
elif test -r /usr/include/linux/socket.h ; then
        V_PCTL=linux
elif test -c /dev/bpf0 ; then           # check again in case not readable
        V_PCTL=bpf
elif test -c /dev/enet ; then           # check again in case not readable
        V_PCTL=enet
elif test -c /dev/nit ; then            # check again in case not readable
        V_PCTL=snit
else
        V_PCTL=null
fi

AC_MSG_RESULT($V_PCTL)
AC_SUBST(V_PCTL)

AC_PROG_INSTALL
AC_PROG_MAKE_SET

# Checks for libraries.
# FIXME: Replace `gettext' with a function in `-lintl':
AC_CHECK_LIB(intl, gettext)
dnl If any socket libraries needed
AC_CHECK_FUNC(gethostent, , AC_CHECK_LIB(nsl, gethostent))
AC_CHECK_FUNC(setsockopt, , AC_CHECK_LIB(socket, setsockopt))
dnl need posix4/nanosleep for solaris 2.4
AC_CHECK_FUNC(nanosleep, , AC_CHECK_LIB(posix4, nanosleep))

# Checks for header files.
AC_HEADER_STDC
AC_CHECK_HEADERS(netinet/ether.h arpa/inet.h libintl.h netdb.h\
                 netinet/in.h netinet/if_ether.h\
	         stdlib.h string.h netinet/ether.h\
                 strings.h sys/param.h sys/socket.h unistd.h\
                 net/ethernet.h net/ethertypes.h net/if_arp.h\
                 net/if.h net/if_ether.h errno.h sys/ioctl.h\
                 linux/if_ether.h sys/time.h fcntl.h)

# Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST
AC_TYPE_SIZE_T

# Checks for library functions.
AC_PROG_GCC_TRADITIONAL
AC_FUNC_MALLOC
AC_TYPE_SIGNAL
AC_FUNC_STRERROR_R
AC_FUNC_VPRINTF
AC_CHECK_FUNCS(bzero gethostbyaddr gethostbyname gethostname inet_ntoa\
              memset socket strerror strtol)

AC_CONFIG_FILES(Makefile)
AC_OUTPUT
